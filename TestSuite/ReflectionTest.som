"

$Id: ReflectionTest.som 30 2009-07-31 12:20:25Z michael.haupt $

Copyright (c) 2007 Michael Haupt, Tobias Pape
Software Architecture Group, Hasso Plattner Institute, Potsdam, Germany
http://www.hpi.uni-potsdam.de/swa/

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the 'Software'), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED 'AS IS', WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
"

ReflectionTest = (
    run: harness = (
        | tmp o |
        
        (Object methods at: 1) signature = #class ifFalse: [
            harness
                fail: self
                because: 'The first method in Object is not #class. ' +
                         'It is ', (Object methods at: 1) signature asString.].
        (Object hasMethod: #==) ifFalse: [
            harness
                fail: self
                because: 'Object should have the method #==.' ].
        (Object new respondsTo: #isNil) ifFalse: [
            harness
                fail: self
                because: 'An Object instance should respond to #isNil.' ].
        (23 respondsTo: #isNil) ifFalse: [
            harness
                fail: self
                because: 'Any object should respond to #isNil (even 23).' ].
        (23 respondsTo: #+) ifFalse: [
            harness
                fail: self
                because: '23 should respond to #+.' ].
        
        "Testing #perform"
        tmp := 23 perform: #class.
        
        tmp = Integer ifFalse: [
            harness
                fail: self
                because: '`23 perform: #class` shoud return Integer, but returned ', tmp asString. ].
                
        tmp := 23 perform: #between:and: withArguments: (Array with: 22 with: 24).
        tmp ifFalse: [
            harness
                fail: self
                because: '#between:and: seems to have failed when it was applied with #perform:withArguments:'.
        ].
        
        o := SuperTest new.
        tmp := o perform: #something inSuperclass: SuperTestSuperClass.
        tmp = #super ifFalse: [
            harness
                fail: self
                because: 'Perform was not executed in expected super class'.
        ].
        
        "Trying to see whether the stack works properly"
        tmp := (23 perform: #class) = Integer ifTrue: [#a] ifFalse: [#b].
        tmp = #a ifFalse: [
            harness
                fail: self
                because: 'There seems to be something fishy going on with the stack. tmp = ' + tmp asString ].
        
        tmp := 5 + (23 perform: #value).
        tmp = 28 ifFalse: [
            harness
                fail: self
                because: 'There seems to be something fishy going on with the stack. tmp = ' + tmp asString ].
                    
        
        "Testing #at: and #at:put:"
        tmp := Pair withKey: 3 andValue: 42.
        (tmp instVarAt: 3) = tmp key ifFalse: [  "REM: this might be different, depending on the number of fields in Object!!!"
            harness
                fail: self
                because: '#at: did not return the expected value from the Pair object.'
        ].
        
        tmp instVarAt: 3 put: #foo.
        tmp key = #foo ifFalse: [  "REM: this might be different, depending on the number of fields in Object!!!"
            harness
                fail: self
                because: '#at:put: did not set the correct field in the pair object.'
        ].
        
    )
)
