"

$Id: Array.som 29 2009-07-31 11:28:44Z michael.haupt $

Copyright (c) 2001-2013 see AUTHORS file

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the 'Software'), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED 'AS IS', WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
"

Array = (

    "Accessing"
    at: index            = primitive
    at: index put: value = primitive
    length               = primitive
    putAll: block        = ( self doIndexes: [ :i |
                                self at: i put: block value ] )
    first = ( ^ self at: 1 )
    last  = ( ^ self at: self length )
    
    
    "Iterating"
    do: block            = ( self doIndexes: [ :i |
                                block value: (self at: i) ] )
    doIndexes: block     = ( | i | i := 1. [ i <= (self length) ] whileTrue:
                                [ block value: i. i := i + 1 ] )
                                
    from: start to: end do: block = ( | i |
        i := start.
        [ i <= end ] whileTrue: [
            block value: (self at: i).
            i := i + 1 ]
    )
    
    "Copying (inclusively)"
    copyFrom: start to: end = ( | result i |
        result := Array new: end - start + 1.
        i := 1.
        self from: start to: end do: [ :e |
            result at: i put: e.
            i := i + 1 ].
        ^result
    )
    
    copyFrom: start = ( ^self copyFrom: start to: self length )

    copy = (^self copyFrom: 1)
    
    "Numerical"
    sum     = ( ^self inject: 0 into: [ :sub :elem | sub + elem ] )
    average = ( ^self sum / self length )
                                
    "Containment check"
    contains: element = ( self do: [ :e | e = element ifTrue: [ ^true ] ].
                          ^false )
    
    "Collection"
    collect: aBlock = (
        | result |
        result := Array new: self length.
        self doIndexes: [ :i | result at: i put: (aBlock value: (self at: i)) ].
        ^result
    )

    inject: sub into: aBlock = ( | next |
        next := sub.
        self do: [ :e | next := aBlock value: next with: e ].
        ^next
    )
    
    reject: aBlock = (
        ^ self select: [:element | (aBlock value: element) == false ]
    )
    
    select: aBlock = (
        "TODO: fix the hard reference to Vector..."
        | newCollection |
        newCollection := Vector new: self length.
        self do: [:each | (aBlock value: each)  
                            ifTrue: [newCollection append: each]].
        ^ newCollection
    )
    
    union: aCollection = (
        | new |
        new := Set new.
        new addAll: self.
        new addAll: aCollection.
        ^ new
    )
    
    ----------------------------
    
    "Allocation"
    new                        = ( ^self new: 0 )
    new: length                = primitive
    new: length withAll: block = ( ^((self new: length) putAll: block) )

    "Convenience"
    with: a = (
        | arr |
        arr := self new: 1.
        arr at: 1 put: a.
        ^ arr
    )
    
    with: a with: b            = (
        | arr |
        arr := self new: 2.
        arr at: 1 put: a.
        arr at: 2 put: b.
        ^ arr
    )

    with: a with: b with: c    = (
        | arr |
        arr := self new: 3.
        arr at: 1 put: a.
        arr at: 2 put: b.
        arr at: 3 put: c.
        ^ arr
    )
)
