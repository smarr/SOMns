(* Fork/Join Benchmarks for SOMns *)
class ForkJoinBenchmarks usingPlatform: platform andHarness: harness = (
| private Benchmark = harness Benchmark.
  private Task      = platform threading Task.
  private Vector    = platform kernel Vector.
|)(
  public class Fib = Benchmark (
  | private expected = createExpected.
    private N = 10. |
  )(
    public benchmark = ( 
      ^ fib: N
    )

    public fib: n = (
      | a b ta tb |
      n <= 2 ifTrue: [ ^ 1 ].

      ta := Task spawn: [ fib: n - 1 ].
      tb := Task spawn: [ fib: n - 2 ].

      a := ta join.
      b := tb join.
      ^ a + b
    )

    public verifyResult: result = (
      ^ (expected at: (N + 1)) = result
    )

    private createExpected = (
      | e |
      e := Vector new.
      e, 0, 1, 1, 2, 3, 5, 8, 13, 21, 34, 55, 89, 144, 233,
      377, 610, 987, 1597, 2584, 4181, 6765, 10946, 17711.
      e, 28657, 46368, 75025, 121393, 196418, 317811, 514229,
      832040, 1346269, 2178309, 3524578, 5702887, 9227465.
      e, 14930352, 24157817, 39088169, 63245986, 102334155,
      165580141, 267914296, 433494437, 701408733, 1134903170.
      ^ e asArray
    )
  ) : (
    public newInstance = ( ^ Fib new )
    public setupVerifiedRun: run = ( run innerIterations: 1 )
  )
)
