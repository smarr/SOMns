(* Copyright (c) 2001-2015 see AUTHORS file

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the 'Software'), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED 'AS IS', WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
*)
class BounceSuite usingPlatform: platform andHarness: harness = (
| private Benchmark = harness Benchmark.
  private Random    = harness Random.
  private Array     = platform kernel Array.
|
) (
  class Bounce = Benchmark ()(

    benchmark = (
      | ballCount balls bounces random |
      
      random := Random new.
      
      ballCount := 100.
      bounces   := 0.
      balls     := Array new: ballCount withAll: [ Ball new: random ].
      
      1 to: 50 do: [ :i |
        balls do: [ :ball |
          ball bounce ifTrue: [ bounces := bounces + 1 ] ] ].

      ^ bounces
    )
    
    verifyResult: result = (
      ^ self assert: 1331 equals: result
    )
  )
  
  class Ball new: random = (
    | x    ::= random next % 500.
      y    ::= random next % 500.
      xVel ::= (random next % 300) - 150.
      yVel ::= (random next % 300) - 150.
    |
  ) (
    public bounce = (
      | xLimit yLimit bounced |
      xLimit  := yLimit := 500.
      bounced := false.
    
      x := x + xVel.
      y := y + yVel.
      x > xLimit ifTrue: [
        x := xLimit. xVel := 0 - xVel abs. bounced := true ].
      x < 0 ifTrue: [
        x := 0.      xVel := xVel abs.     bounced := true ].
      y > yLimit ifTrue: [
        y := yLimit. yVel := 0 - yVel abs. bounced := true ].
      y < 0 ifTrue: [
        y := 0.      yVel := yVel abs.     bounced := true ].
      ^ bounced
    )
  )
  
  public newInstance = ( ^ Bounce new )
  public setupVerifiedRun: run = ( run innerIterations: 1 )
)
