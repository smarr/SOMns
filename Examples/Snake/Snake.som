"

$Id: Snake.som 426 2008-05-22 08:22:07Z stefan.marr $

Copyright (c) 2001-2008 see AUTHORS file

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the 'Software'), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED 'AS IS', WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
"

Snake = (
  | head tail board |
  
  head: val = ( head := val )
  tail: val = ( tail := val )
  board: val = ( board := val )
  
  moveLeft = (
    | newPos |
    newPos := SnakeElement newWithX: head x Y: head y.
    newPos x: (self overflow: (newPos x - 1) max: board width).
    ^self move: newPos
  )

  moveRight = (
    | newPos |
    newPos := SnakeElement newWithX: head x Y: head y.
    newPos x: (self overflow: (newPos x + 1) max: board width).
    ^self move: newPos
  )

  moveUp = (
    | newPos |
    newPos := SnakeElement newWithX: head x Y: head y.
    newPos y: (self overflow: (newPos y - 1) max: board height).
    ^self move: newPos
  )

  moveDown = (
    | newPos |
    newPos := SnakeElement newWithX: head x Y: head y.
    newPos y: (self overflow: (newPos y + 1) max: board height).
    ^self move: newPos
  )

  move: newPos = (
    newPos next: head.
    head prev: newPos.
    head := newPos.

    (board isAppleAtX: (newPos x) Y: (newPos y)) ifTrue: [
      board addApple
    ] ifFalse: [

      (board isSnakeAtX: newPos x Y: newPos y) ifTrue: [
        ^false
      ].
      board remove: tail.
      tail := tail prev.
      tail next: nil.
    ].
    board add: newPos.

    ^true
  )
	
  overflow: val max: max = (
    (val < 1) ifTrue: [ val := max + val ].
    (val > max) ifTrue: [ val := val - max ].
    ^val
  )

  ----
  newWithX: x Y: y andBoard: board = (
    | newSnake head |
    newSnake := Snake new.
    head := SnakeElement newWithX: x Y: y.
    
    newSnake head: head.
    newSnake tail: head.
    newSnake board: board.
    board add: head.
    ^newSnake
  )
)
