"


Copyright (c) 2001-2013 see AUTHORS file

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the 'Software'), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED 'AS IS', WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
"

Benchmark = (
    | numIterations numWarmUpIterations innerIterations printIndividualRuns maxRuntime total |
    
    total = ( ^ total )
    numIterations = ( ^ numIterations )
    innerIterations = ( ^ innerIterations )
    
    maxRuntime: seconds = (
        "converted to microseconds i.e. ticks"
        seconds ifNotNil: [
            maxRuntime := seconds * 1000 * 1000].
    )

    run: numIter with: numWarmUpIter and: numInner printAll: aBool = (
        numIterations       := numIter.
        numWarmUpIterations := numWarmUpIter.
        innerIterations     := numInner.
        printIndividualRuns := aBool.
        
        self exec.
    )

    run = ( 
        | harness |
        harness := All new.
        harness initialize.
        harness benchmarkClasses: (Vector with: self class).
        harness printAll: false.
        harness maxRuntime: 3. "seconds"
        harness numIterations: 100.
        harness warmUp: 10.

        harness runBenchmarks.
        harness printTotal.
    )
    
    warmup = (
        | numIterationsTmp printIndividualRunsTmp maxRuntimeTmp |
        numWarmUpIterations > 0 ifFalse: [
            '' println.
            ^ self].

        numIterationsTmp := numIterations.
        printIndividualRunsTmp := printIndividualRuns.
        maxRuntimeTmp := maxRuntime.

        numIterations := numWarmUpIterations.
        printIndividualRuns := false.
        maxRuntime := nil.

        ' warmup ...' print.
        self doRuns.

        numIterations := numIterationsTmp.
        printIndividualRuns := printIndividualRunsTmp.
        maxRuntime := maxRuntimeTmp.

        ' completed.' println.
    )

    exec = (
        (('Starting ' + (self name)) + ' benchmark ... ') print.
        self warmup.
        total := self doRuns.
    )
    
    doRuns = (
        | i total |
        i := 0.
        total := 0.
        
        [ i < numIterations ] whileTrue: [
            | startTime endTime runTime |
            startTime := system ticks.
            self innerBenchmarkLoop.
            endTime   := system ticks.
            
            runTime := endTime - startTime.
            printIndividualRuns ifTrue: [ self printRun: runTime ].
            
            total := total + runTime.
            i := i + 1.
            
            maxRuntime ifNotNil: [
                total > maxRuntime ifTrue: [
                    numIterations := i.
                    ^ total ]].
            
            system fullGC ].
        
        ^ total
    )
    
    innerBenchmarkLoop = (
        | i |
        i := 0.
        [ i < innerIterations ] whileTrue: [
            self benchmark.
            i := i + 1.
        ].
    )
    
    printRun: runTime = (
        self name print.
        ': iterations=1' print.
        ' runtime: ' print.
        runTime print.
        'us' println
    )
    
    benchmark  = ( self subclassResponsibility )
    name       = ( ^self class name asString )
    
)
