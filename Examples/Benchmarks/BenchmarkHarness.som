"
Copyright (c) 2001-2013 see AUTHORS file

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the 'Software'), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED 'AS IS', WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
"

BenchmarkHarness = (

    "The BenchmarkHarness can be invoked on the command line and should be
    passed a list of benchmarks to run (class names) as arguments. It will
    run each of them and output single as well as a total."
    
    | total runtimes benchmarkClasses numIterations warmUp innerIterations numThreads maxRuntime printAll |
    
    benchmarkClasses: classes = ( benchmarkClasses := classes )
    printAll: aBool = ( printAll := aBool )
    maxRuntime: seconds = ( maxRuntime := seconds )
    numIterations: anInt = (numIterations := anInt)
    numThreads:    anInt = (numThreads := anInt)
    warmUp: anInt = (warmUp := anInt)
    
    total = ( ^ total )
    
    run: args = (
        args length < 2 ifTrue: [ ^ self printUsage ].
        
        self initialize.
        self processArguments: args.

        self runBenchmarks.
        self printTotal.
    )
    
    initialize = (
        total         := 0.
        runtimes      := Dictionary new.
        numIterations := 1.
        numThreads    := 1.
        warmUp        := 0.
        innerIterations := 1.
        maxRuntime    := nil.
        printAll      := true.
    )
    
    printUsage = (
      './som.sh -cp Smalltalk Examples/Benchmarks/BenchmarkHarness.som [benchmarks] [num-iterations [warm-up [inner-iter [num-threads]]]]' println.
      '' println.
      '  benchmarks     - list of benchmark class names (e.g.: Queens Fibonacci Dispatch)' println.
      '  num-iterations - number of times each benchmark is executed, default: 1' println.
      '  warm-up        - number of times each benchmark is executed before measuring, default: 0' println.
      '  inner-iter     - number of times each benchmark is executed in an inner loop, which is measured in total, default: 1' println.
      '  num-threads    - number of threads executing, each doing num-iterations, default: 1' println.
    )
    
    processArguments: args = (
        | remainingArgs |
        remainingArgs := self collectBenchmarkClasses: args.
        remainingArgs ifNotNil: [
            numIterations := (remainingArgs at: 1) asInteger.
            remainingArgs length > 1 ifTrue: [
                warmUp := (remainingArgs at: 2) asInteger.
                remainingArgs length > 2 ifTrue: [
                    innerIterations := (remainingArgs at: 3) asInteger.
                    remainingArgs length > 3 ifTrue: [
                        numThreads := (remainingArgs at: 4) asInteger.
                    ]
                ].
            ].
        ]
    )
    
    collectBenchmarkClasses: args = (
        | i benchmarks |
        i := 2. "First argument is the BenchmarkHarness"
        
        benchmarks := Vector new: args length - 1.
        [i <= args length] whileTrue: [
            | sym cls |
            sym := (args at: i) asSymbol.
            cls := system load: sym.
            cls ifNil: [
                "This item does not seem to be a class, so it is most likely
                 one of the other parameters"
                 benchmarkClasses := benchmarks.
                 benchmarks size = 0 ifTrue: [ self error: 'No benchmark found' ].
                 ^ args copyFrom: i to: args length ].
            cls := system resolve: sym.
            benchmarks append: cls.
            i := i + 1 ].
        benchmarks size = 0 ifTrue: [ self error: 'No benchmark found' ].

        benchmarkClasses := benchmarks.
        ^ nil
    )

    executeBenchmark: benchmark = (
        | bench |
        bench := benchmark new.
        bench maxRuntime: maxRuntime.

        bench run: numIterations with: warmUp and: innerIterations printAll: printAll.
        total := total + bench total.
        runtimes at: benchmark put: bench total.
        self reportRun: bench.
    )

    runBenchmarks = (
        numThreads = 1
            ifTrue:  [
                benchmarkClasses do: [ :benchmark |
                    self executeBenchmark: benchmark ]]
            ifFalse: [
                | threads |
                threads := Array new: numThreads.
                1 to: numThreads do: [:i |
                    threads at: i put: (
                        [benchmarkClasses do: [ :benchmark |
                            self executeBenchmark: benchmark ]] spawn).
                ].
                
                1 to: numThreads do: [:i | 
                    (threads at: i) join.
                ]
            ]
    )
    
    reportRun: bench = (
        | out |
        out :=       bench name.
        out := out + ': iterations='.
        out := out + bench numIterations.
        out := out + ' average: '.
        out := out + (bench total / bench numIterations).
        out := out + 'us total: '.
        out := out + bench total.
        out := out + 'us'.
        out println.
        '' println.
    )
        
    printTotal = (
        ('Total Runtime: ' + total asString + 'us') println.
    )

)
